#
#	SConstruct file for Sodalis Project server
#
#	Fri Apr  7 17:08:38 2006
#		J. Anton, Copyright 2006
#

import sys

Help("""
Build the server: 'scons prefix=... use=... mode=... debug=...`
Clear the built server: 'scons -c`
Install the server: 'scons install prefix=... use=... mode=... debug...`
	WARNING: server might be rebuilt firstly

========
Install prefix: path, where the server will be installed; default: /usr/local

Special abilities' list; default: gettext
	'gettext`: use gettext library

Build mode: debug|release; default: debug
	'debug`: build debug version, debug mode can be set
	'release`: build the release version, debug mode setting will not be used
	
Debug mode: memory,net,db,func,log; default: all of them
	'memory`: watch memory allocations
	'net`: log all network messages
	'db`: log all SQL requests
	'func`: watch all function calls (entry and exit points)
	'log`: write all log messages on the screen besides the log file
	""")

baseenv=Environment(CPPPATH=[".","../kaneutil2"])
env=baseenv.Copy(
	CCFLAGS="-Wall"
	)

mode=ARGUMENTS.get("mode","debug")
if mode=="debug":
	env.Append(CCFLAGS=" -g -DDEBUG")
	debug=ARGUMENTS.get("debug","memory,net,db,func,log")
	if "memory" in debug:
		env.Append(CCFLAGS=" -DDEBUG_MEMORY")
	if "net" in debug:
		env.Append(CCFLAGS=" -DLOG_NET")
	if "db" in debug:
		env.Append(CCFLAGS=" -DLOG_DB")
	if "func" in debug:
		env.Append(CCFLAGS=" -DDEBUG_FUNC")
	if "log" in debug:
		env.Append(CCFLAGS=" -DDEBUG_LOG")
elif mode=="release":
	env.Append(CCFLAGS=" -O3")
	env.Append(LINKFLAGS=" -s")
	debug=""
else:
	print "Invalid mode ( debug|release )"
	sys.exit()

use=ARGUMENTS.get("use","gettext")
if "gettext" in use:
	env.Append(CCFLAGS=" -DUSE_GETTEXT")

prefix=ARGUMENTS.get("prefix","/usr/local")
env.Append(CCFLAGS=" -DPREFIX="+prefix)

sources = Split("""
	main/sodalis.c
	main/tools.c
	network/net.c
	network/nstore.c
	user/user.c
	user/utree.c
	database/db.c
	parse/parse.c
	""")

libs = Split("""
	ku2
	mysqlclient
	ability
	Magick
	""")

libpaths = Split("""
	../kaneutil2
	/usr/lib/mysql
	ability
	""")

sodalis_flags=env["CCFLAGS"]
SConscript("ability/SConstruct","sodalis_flags")

sodalisd=env.Program("sodalisd",sources,LIBS=libs,LIBPATH=libpaths)
Default(sodalisd)

env.Alias("installserver",prefix)
env.Install(
	prefix+"/bin/",
	sodalisd)
env.Install(
	prefix+"/etc/",
	["sodalis.cfg"])
